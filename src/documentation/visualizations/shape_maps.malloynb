>>>markdown

# Shape Maps

The plugin currently supports US maps and state names. The model and data styles for the subsequent examples are:

>>>malloy
source: airports is duckdb.table('../data/airports.parquet') {
  primary_key: code
  measure: airport_count is count()
  query: by_state is {
    group_by: state
    aggregate: airport_count
  }
  # shape_map
  query: by_state_shaped is by_state
}
>>>markdown


## Run a query and tag the reusults as a shape map

We can explicitly return a result as a shape map.

>>>malloy
#(docs) size=large
# shape_map
run: airports -> by_state
>>>markdown

## The tag is in the semantic model 

In the query below the tag is in the semantic model.

>>>malloy
#(docs) size=large
run: airports -> by_state_shaped {where: fac_type='SEAPLANE BASE'}
>>>markdown


## Run as a trellis
By calling the configured map as a nested subtable, a trellis is formed.


>>>malloy
#(docs) size=large
run: airports -> {
  group_by: faa_region
  aggregate: airport_count
  # shape_map
  nest: by_state
}
>>>markdown

## Run as a trellis, repeated with different filters

>>>malloy
#(docs) size=large
run: airports -> {
  group_by: faa_region
  aggregate: airport_count
  nest:
    // shape map is declared in the model
    heliports is by_state_shaped { where: fac_type = 'HELIPORT' }
    # shape_map
    seaplane_bases is by_state { where: fac_type = 'SEAPLANE BASE' }
}
>>>markdown
