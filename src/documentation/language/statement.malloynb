>>>markdown
# File Structure

A Malloy file (`.malloy`) is a collection of [statements](#statements), [comments](#comments), and [tags](#tags). Statements can be separated with an optional semi-colon for clarity, which helps readability when statements appear on the same line.

Malloy recognizes modeling as a key aspect of data analytics and provides tools for modularity and reusability. Whereas SQL queries generally define all metrics inline, in Malloy, _dimensions_, _measures_, and _views_ can be saved and attached to a modeled source, which itself is defined as part of a Malloy file.

## Statements

There are four kinds of statements that can appear in a Malloy file:

* [Import Statements](#import-statements)
* [Query Statements](#query-statements)
* [Source Statements](#source-statements)
* [Run Statements](#run-statements)


### Import Statements

Import statements allow you to import sources and queries from another `.malloy` file.
>>>malloy
import "flights.malloy"
>>>markdown

#### Import Locations

Imported files may be specified with relative or absolute URLs.

| Import Statement | Meaning from `"file:///f1/a.malloy"` |
| ---------------- | --------|
| `import "b.malloy"` | `"file:///f1/b.malloy"` |
| `import "./c.malloy"` | `"file:///f1/c.malloy"` |
| `import "/f1/d.malloy"` | `"file:///f1/d.malloy"` |
| `import "file:///f1/e.malloy"` | `"file:///f1/e.malloy"` |
| `import "../f2/f.malloy"` | `"file:///f2/f.malloy"` |
| `import "https://example.com/g.malloy"` | `"https://example.com/g.malloy"` |

#### Selective Imports

By default, all objects from the referenced file are imported. You can use `{} from` to select (and optionally rename) specific objects:
>>>malloy
import { airports, spaceports is airports } from "airports.malloy"

run: airports -> { aggregate: airport_count is count() }
run: spaceports -> { aggregate: spaceport_count is count() }
>>>markdown

### Query Statements

A query statement defines a query as a named entity within a model, allowing it to be reused as the basis for other queries.
>>>malloy
query: flights_by_carrier is duckdb.table('../data/flights.parquet') -> {
  group_by: carrier
  aggregate: flight_count is count()
}
>>>markdown

See the [Queries](query.malloynb) section for more information on queries.

### Source Statements

In Malloy, a source is the basic unit of reusability for calculations, join relationships, and queries, and can be thought of as a table and a collection of computations and relationships which are relevant to that table.

A source statement defines a source as a reusable part of the model.
>>>malloy
source: flights2 is duckdb.table('../data/flights.parquet') extend {
  dimension: distance_km is distance * 1.609344

  measure: flight_count is count()

  view: by_carrier is {
    group_by: carrier
    aggregate: flight_count
  }
}
>>>markdown

See the [Sources](source.malloynb) section for more information on sources.

### Run Statements

Run statements allow you to write queries in a model without naming them, or to indicate to the host application that a particular query should be run. You'll see run statements all over the documentation, usually followed by the results of running that query. 

Any query can be run by including it in a run statement.
>>>malloy
#(docs) html
run: flights2 -> { 
  group_by: origin
  aggregate: destination_count is count(destination) 
}
>>>markdown

See the [Queries](query.malloynb) section for more information on queries.

## Comments

Comments in Malloy can be written with `--` (as in SQL) or `//`. A comment continues until the end of the line.
>>>malloy
// This is a comment
-- This is also a comment
>>>markdown

## Tags

Tags are a general-purpose feature of Malloy that allow arbitrary metadata to be attached to various Malloy objects (queries, sources, fields, etc.). One use case for tagging is to attach rendering information to a query:
>>>malloy
# json
run: flights2 -> {
  group_by: carrier
  aggregate: flight_count
  limit: 2
}
>>>markdown

For more information about the rendering tags used in the Malloy rendering library, see the [Visualizations](../visualizations/overview.malloynb) section.

For more information about tag semantics, see the [Tags](./tags.malloynb) section.