-- --! styles airports.styles.json

source: airports is duckdb.table('../data/airports.parquet') extend {
  primary_key: code

  measure: airport_count is count()

  dimension: name is concat(code, ' - ', full_name)
  dimension: faa_region_name is faa_region ?
    pick 'Southwest' when 'ASW'
    pick 'Northwest Mountain' when 'ANM'
    pick 'Eastern' when 'AEA'
    pick 'Southern' when 'ASO'
    pick 'Great Lakes' when 'AGL'
    pick 'Central' when 'ACE'
    pick 'New England' when 'ANE'
    pick 'Western Pacific' when 'AWP'
    pick 'Alaska' when 'AAL'

  view: by_region is {
    group_by: faa_region
    group_by: faa_region_name
    aggregate: airport_count
  }

  view: by_state is {
    where: state != null
    group_by: state
    aggregate: airport_count
  }

  view: by_facility_type is {
    group_by: fac_type
    aggregate:
      airport_count
      avg_elevation is elevation.avg()
  }

  view: major_airports is {
    where: major = 'Y'
    group_by: name is concat(state, '-', full_name)
  }

  view: airports_by_region_dashboard is by_region + {
    where: faa_region != null
    nest:
      # shape_map
      by_state
      by_facility_type
      major_airports
  }
}
