>>>markdown
# Ordering and Limiting

Often when querying data the amount of data returned to look at is much smaller than the full result set, so the ordering of the data makes a big difference in what you actually see. To make things easier, Malloy has some smart defaults in the way it presents data.  For the most part, you don't have to think too much about it, but in order to understand it, this document will show you how Malloy makes decisions about what to show you.


## Implicit Ordering

### Rule 1: Newest first
If a query stage has a [dimensional](dimensions.malloynb) column that represents a point in time, it is usually the most
important concept in the query.  Because the most recent data is usually the most relevant, Malloy sorts the newest data first.
>>>malloy
##(docs) hidden
import "flights.malloy"
>>>malloy
run: flights -> {
  group_by: dep_month is dep_time.month
  aggregate: flight_count is count()
}
>>>markdown

### Rule 2: Largest first
If there is a [measure](aggregates.malloynb) involved, Malloy sorts larger values first.

In the following example, Rule 1 doesn't apply, so the default behavior is to sort by first aggregate, `flight_count` with the largest values first.
>>>malloy
run: flights -> {
  group_by: carrier
  aggregate: flight_count is count()
}
>>>markdown

## Explicit Ordering

You can be explicit about result ordering by using the `order_by` clause.

In the following example, the results are ordered by `carrier` in reverse alphabetical order.
>>>malloy
run: flights -> {
  order_by: carrier desc
  group_by: carrier
  aggregate: flight_count is count()
}
>>>markdown

Like in SQL, Malloy's `order_by` always defaults to ascending order when `desc` is omitted. This is true for any column of any type. In the example below,
the results are ordered by `carrier` in alphabetical order.
>>>malloy
run: flights -> {
  order_by: carrier
  group_by: carrier
  aggregate: flight_count is count()
}
>>>markdown
Malloy also supports lists of items (using commas or line breaks) in the `order_by: ` clause. Malloy will sort by the first entry first, then by the second within the first, etc.

You cannot have multiple `order_by:` clauses within a query. When an `order_by: ` is applied in a refinement, it will fully override whatever the previous ordering was.
>>>malloy
// This query will only be ordered by total_distance and carrier due to the refinement.
run: flights -> {
  group_by: flight_num, carrier
  order_by:
    flight_num desc
    total_distance
    flight_count desc
  aggregate: flight_count is count()
  aggregate: total_distance
} + {
    order_by: total_distance, carrier
}
>>>markdown

## Limiting

In Malloy, you can limit the number of results returned using `limit: <<integer literal>>` or `top: <<integer literal>>`.  Both are provided for readability.

In the example below, the results are limited to 2 rows, which are sorted by `dep_month` with newest results first (due to Rule 1).
>>>malloy
run: flights -> {
  limit: 2
  group_by: dep_month is dep_time.month
  aggregate: flight_count is count()
}
>>>markdown
