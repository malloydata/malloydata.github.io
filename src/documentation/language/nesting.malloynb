>>>markdown
# Nested Queries

Nested queries, more formally known as "aggregating subqueries" are queries included in other queries. A nested query produces a subtable per row in the query in which it is embedded. In Malloy, queries can be named and referenced in other queries. The technical term "aggregating subquery" is a bit of a mouthful, so we more often refer to it as a "nested query."

When a query is nested inside of another query, it produces an aggregating subquery and the results include a nested subtable.

>>>malloy
##(docs) hidden
import "airports.malloy"
>>>malloy
run: airports -> {  
  group_by: state
  aggregate: airport_count
  nest: by_facility is -> {
    group_by: fac_type
    aggregate: airport_count
  }
}
>>>markdown

## Nesting Nested Queries

Aggregating subqueries can be nested infinitely, meaning that a nested query can contain another nested query.

>>>malloy
run: airports -> {
  group_by: state
  aggregate: airport_count
  nest: top_5_counties is -> {
    limit: 5
    group_by: county
    aggregate: airport_count
    nest: by_facility is -> {
      group_by: fac_type
      aggregate: airport_count
    }
  }
}
>>>markdown

## Filtering Nested Queries

Filters can be applied at any level within nested queries.

>>>malloy
run: airports -> {
  where: state ? 'CA' | 'NY' | 'MN'
  group_by: state
  aggregate: airport_count
  nest: top_5_counties is {
    limit: 5
    group_by: county
    aggregate: airport_count
    nest: major_facilities is -> {
      where: major = 'Y'
      group_by: name is concat(code, ' - ', full_name)
    }
    nest: by_facility is -> {
      group_by: fac_type
      aggregate: airport_count
    }
  }
}
>>>markdown
