>>>markdown
# Malloy by Example

This document will assumes a working knowledge of SQL and will rapidly take you through some of
Malloy's key language features.

## Using this Guide

For every Malloy Query you can see the formatted result, or raw result as JSON, or the SQL used to produce the result.

Click tab to to see the  HTML, JSON or SQL result:  <img src="https://user-images.githubusercontent.com/1093458/154121968-6436d94e-94b2-4f16-b982-bf136a3fcf40.png" style="width:142px"> ðŸ‘ˆðŸ‘ˆ

## SQL SELECT vs Malloy's `run:`

The statement to run a query in Malloy is `run:`. There are two types of queries in Malloy, reductions which have `group_by:` or `aggregate:` statements, and projections which have `select:` statements and do not group or aggregate results.

### Projection: SELECT with no GROUP BY

In SQL
```sql
SELECT code, full_name, state, faa_region, fac_type, elevation
FROM `malloy-data.faa.airports`
ORDER BY code
```
>>>markdown
Equivalent in Malloy
>>>malloy
run: duckdb.table('../data/airports.parquet') -> {
  select: code, full_name, state, faa_region, fac_type, elevation
  order_by: code
}
>>>markdown

### Reduction: SELECT with GROUP BY and/or aggregation

In SQL
```sql
SELECT
  base.fac_type as fac_type,
  COUNT( 1) as airport_count
FROM `malloy-data.faa.airports` as base
WHERE base.state='CA'
GROUP BY 1
ORDER BY 2 desc
```
>>>markdown

Equivalent in Malloy
>>>malloy
run: duckdb.table('../data/airports.parquet') -> {
  group_by: fac_type
  aggregate: airport_count is count()
  where: state = 'CA'
  order_by: airport_count desc
}
>>>markdown

## Source: A data source for queries

Malloy separates a query's view from the source of the data. A source can be thought of as a table and a collection of computations and relationships which are relevant to that table.  ([Source Documentation](../language/source.malloynb)).

[Fields](../language/fields.malloynb) can be defined as part of a source.


* A `measure:` is a declared aggregate calculation (think function that operates across the table) which can be used in `aggregate:` elements in a query stage
* A `dimension:` is a declared scalar calculation which that can be used in `group_by:` or `select:` elements of a query stage
>>>malloy
source: airports is duckdb.table('../data/airports.parquet') extend {
  dimension: elevation_in_meters is elevation * 0.3048
  dimension: state_and_county is concat(state,' - ', county)
  measure: airport_count is count()
  measure: avg_elevation_in_meters is elevation_in_meters.avg()
}
>>>markdown

## Querying Against a Source

Queries can be run against `source:` objects and can utilize the modeled fields from that source, as well as introduce new ones. ([Query Documentation](../language/query.malloynb))


*using the above declared `airports` source*
>>>malloy
run: airports -> {
  limit: 10
  where: fac_type = 'HELIPORT'
  group_by: state
  aggregate:
    airport_count           // <-- declared in source
    avg_elevation_in_meters // <-- declared in source
}
>>>markdown

## Dimensional calculations are no different from columns

*using the above declared `airports` source*
>>>malloy
run: airports -> {
  group_by: state_and_county // <-- declared in source
  aggregate: airport_count
  order_by: 1 desc
}
>>>markdown


## Defining Views in a Source

A source can also contain a set of useful views relating to that source, which can be run in queries.

*using the above declared `airports` source*
>>>malloy
source: airports2 is duckdb.table('../data/airports.parquet') extend {
  measure: airport_count is count()

  view: by_state is {        // <-- can be called by name
    group_by: state
    aggregate: airport_count
  }
}
>>>markdown

###  Executing Views

The simplest form of a query in Malloy is the name of a source, the query operator `->`, and the name of one of its contained views.

*using the above declared `airports` source*
>>>malloy
run: airports2 -> by_state
>>>markdown

## Filtering a Source

You can filter a source by adding a filter expression using the `where:` keyword in an extension. In this example, we filter an existing `airports` source and then use this extended version to execute the `by_state` view in a query. For more information on filtering, see the [Filters](../language/filters.malloynb) section.
>>>malloy
run: airports2 extend {
  where: fac_type = 'SEAPLANE BASE'   // <- run the query with an added filter
}
-> by_state
>>>markdown

## Filtering Measures

The input to an aggregate computation can be filtered.

*using the above declared `airports` source*
>>>malloy
run: airports -> {
  group_by: state
  aggregate: airport_count
  aggregate: heliport_count is airport_count { where: fac_type = 'HELIPORT' } // <-- add a filter
}
>>>markdown

## Composing with Views

For the next section assume the following source declaration.
>>>malloy
source: airports3 is duckdb.table('../data/airports.parquet') extend {
  measure: airport_count is count()
  measure: avg_elevation is elevation.avg()

  view: top_5_states is {
    group_by: state
    aggregate: airport_count
    limit: 5
  }

  view: by_facility_type is {
    group_by: fac_type
    aggregate: airport_count
  }
}
>>>markdown

## The `nest:` property embeds one view in another

Malloy allows you to create nested subtables easily in a query.
In the case below, the top level view groups by state and the nested view groups by facility type.
This mechanism is really useful for understanding data and creating complex data structures. ([Nesting Documentation](../language/nesting.malloynb))

*using the above declared `airports` source*
>>>malloy
run: airports3 -> {
  group_by: state
  aggregate: airport_count
  limit: 5
  nest: by_facility_type is  {
    group_by: fac_type
    aggregate: airport_count
  }
}
>>>markdown

Queries can contain multiple nested views.

*using the above declared `airports` source*
>>>malloy
run: airports3 -> {
  group_by: faa_region
  aggregate: airport_count
  nest: top_5_states
  nest: by_facility_type
}
>>>markdown

Views can be nested to any level of depth.

*using the above declared `airports` source*
>>>malloy
run: airports3 -> {
  group_by: faa_region
  aggregate: airport_count
  nest: by_state_and_county is  {
    group_by: state
    aggregate: airport_count
    nest: by_county is  {
      group_by: county
      aggregate: airport_count
      limit: 4
    }
  }
  nest: by_facility_type
}
>>>markdown

## Refining a View

The gesture `+ { <<refinements>> }` allows you to base a new view on an existing view while adding new refinements to the query terms.

For example we can add a limit and an order by to `by_state`
>>>malloy
run: airports2 -> by_state + {
  order_by: state desc    // <-- add order by to query
  limit: 2
}
>>>markdown

is the same as
>>>malloy
run: airports -> {
  group_by: state
  aggregate: airport_count
  order_by: state desc
  limit: 2
}
>>>markdown

### You can add a measure or dimension
>>>malloy
run: airports3 -> by_facility_type + {
  aggregate: avg_elevation
}
>>>markdown

### You can nest another view
>>>malloy
run: airports3 -> top_5_states + {
  nest: by_facility_type
}
>>>markdown

Changing the inner and outer query in the example above reveals very different information.
>>>malloy
run: airports3 -> by_facility_type + {
  nest: top_5_states
}
>>>markdown


## Joining

First let's model some simple tables... ([Join Documentation](../language/join.malloynb))

### Carrier table
*simple source declaration used in example below*
>>>malloy
source: carriers is duckdb.table('../data/carriers.parquet') extend {
  measure: carrier_count is count()
}

run: carriers -> {
  select: *
}
>>>markdown

### Flights table

*simple source declaration used in example below*
>>>malloy
source: flights is duckdb.table('../data/flights.parquet') extend {
  measure: flight_count is count()
}

run: flights -> {
  select: id2, tail_num, dep_time, carrier, origin, destination, distance, dep_delay
  limit: 10
}
>>>markdown

## Declare a Join

Join carriers to flights.  Each flight has one carrier so we use `join_one:`.
([Join Documentation](../language/join.malloynb))
>>>malloy
source: carriers2 is duckdb.table('../data/carriers.parquet') extend {
  measure: carrier_count is count()
}

source: flights2 is duckdb.table('../data/flights.parquet') extend {
  join_one: carriers2 on carrier = carriers2.code

  measure:
    flight_count is count()
    total_distance is distance.sum()
    avg_distance is distance.avg()
}
>>>markdown

###  Query the joined tables

*using the above declared `flights` source*
>>>malloy
run: flights2 -> {
  group_by: carriers2.nickname
  aggregate:
    flight_count
    total_distance
    avg_distance
}
>>>markdown

## Aggregates can be computed from anywhere in the Join Tree

([Aggregate Documentation](../language/aggregates.malloynb))


*using the above declared `flights` source*
>>>malloy
run: flights2 -> {
  limit: 10
  group_by: origin
  aggregate: carriers2.carrier_count   // <-- calculation in joined table
  nest: top_3_carriers is  {
    limit: 3
    group_by: carriers2.nickname
    aggregate:
      flight_count
      total_distance
      avg_distance
  }
}
>>>markdown

## More Complex Joins

The most common join pattern is a foreign key join. Malloy uses the `with:`
to declare these and generates more efficient SQL when these joins are used.

In the example below, we use a `with:` join for `carriers` and then model the more complex relationship with the `flights` originating from each `airport` using  `on:`.

Many `flights` have the same
`airport` as their origin so we use `join_many:`.
>>>malloy
source: carriers4 is duckdb.table('../data/carriers.parquet') extend {
  primary_key: code
  measure: carrier_count is count()
}

source: flights4 is duckdb.table('../data/flights.parquet') extend {
  join_one: carriers4 with carrier  // <-- each flight has 1 carrier

  measure:
    flight_count is count()
    total_distance is distance.sum()
    avg_distance is distance.avg()
}

source: airports4 is duckdb.table('../data/airports.parquet') extend {
  join_many: flights4 on code = flights4.origin  // <-- each airport has many flights

  measure: airport_count is count()
  dimension: elevation_in_meters is elevation * 0.3048
  measure: avg_elevation_in_meters is elevation_in_meters.avg()

  view: by_state is {
    group_by: state
    aggregate: airport_count
  }
}
>>>markdown

## Calculations work properly regardless of where you are in the graph

This query is very difficult to express in SQL. Malloy's understanding of source relationships allows it to compute aggregate computations at any node of the join path, unlike SQL which can only do aggregate computation at the. outermost level.
([Aggregate Documentation](../language/aggregates.malloynb))

*using the above declared `airports` source*
>>>malloy
run: airports4 ->  {
  group_by: state
  aggregate:
    flights4.carriers4.carrier_count  // <-- 3 levels
    flights4.flight_count
    flights4.total_distance
    airport_count
    avg_elevation_in_meters         // <-- symmetric calculation
}
>>>markdown

## Pipelines

The output of a query can be used as the source for the next query.

*Assume the following query as a starting point.*
>>>malloy
source: airports5 is duckdb.table('../data/airports.parquet') extend {
  measure: airport_count is count()
}

run: airports5 -> {
  where: fac_type = 'HELIPORT'
  group_by: state
  aggregate: airport_count
  nest: top_3_county is {
    limit: 3
    group_by: county
    aggregate: airport_count
  }
}
>>>markdown

## Un-nesting in a pipeline flattens the table

Queries can be chained together (pipelined), the output of one becoming the input of the next one, by simply adding another `->` operator and a new query definition.
>>>malloy
source: airports6 is duckdb.table('../data/airports.parquet') extend {
  measure: airport_count is count()
}

run: airports6 -> {
  where: fac_type = 'HELIPORT'
  group_by: state
  aggregate: airport_count
  nest: top_3_county is {
    limit: 3
    group_by: county
    aggregate: airport_count
  }
} -> {
  select:
    state
    top_3_county.county
    airports_in_state is airport_count
    airports_in_county is top_3_county.airport_count
    percent_of_state is top_3_county.airport_count / airport_count
}
>>>markdown

## Pipelines can be named as views in sources

Pipelines can do pretty complex things.  They can be built into source objects.
>>>malloy
source: airports7 is duckdb.table('../data/airports.parquet') extend {
  measure: airport_count is count()
  view: county_rollup is {
    where: fac_type = 'HELIPORT'
    group_by: state
    aggregate: airport_count
    nest: top_3_county is {
      limit: 3
      group_by: county
      aggregate: airport_count
    }
  } -> {
    select:
      state
      top_3_county.county
      airports_in_state is airport_count
      airports_in_county is top_3_county.airport_count
      percent_of_state is top_3_county.airport_count/airport_count
  }
}

run: airports7 -> county_rollup
>>>markdown

## Extending Sources

Similar to query refinement, a source can be extended with the `extend { ... }` gesture to create a new version of the source with additional properties.
>>>markdown
```malloy
source: <<new name>> is <<old name>> extend {
  where: <<some data limit>>
  measure: <<new field declarations>>
}
```
>>>markdown

## Sources based on Queries

Sometimes it is helpful to pre-process data before modeling it out with a source. To do this, you can first define a model-level query using a [query statement](../language/statement.malloynb#query-statements), then [extend](../language/source.malloynb#source-extensions) the query to add reusable definitions.

### Model-Level Queries

Here, we define a model-level query `q_airport_facts` based on the <code>flights.parquet</code> table.
>>>malloy
query: q_airport_facts is duckdb.table('../data/flights.parquet') -> {
  group_by:
    flight_year is dep_time.year
    origin
    carrier
  aggregate:
    num_flights is count()
    distance is distance.sum()
}

run: q_airport_facts
query: q_airport_facts2 is duckdb.table('../data/flights.parquet') -> {
  group_by:
    flight_year is dep_time.year
    origin
    carrier
  aggregate:
    num_flights is count()
    distance is distance.sum()
}
>>>markdown

### Source based on a query

Next, we can define a source based on `q_airport_facts` to add reusable fields and source-level queries.
>>>malloy
source: airport_facts is q_airport_facts extend {
  measure: flight_count is num_flights.sum()
  measure: total_distance is distance.sum()

  view: flights_by_year is {
    group_by: flight_year
    aggregate:
      flight_count
      carrier_count is count(carrier)
      origin_count is count(origin)
  }

  view: flights_by_origin is {
    group_by: origin
    aggregate:
      flight_count
      carrier_count is count(carrier)
  }
}
>>>markdown

### Querying the Summary source
>>>malloy
run: airport_facts -> flights_by_origin
>>>markdown

## Other Interesting Language Features:

### SQL Sources

See the [SQL Sources](../language/sql_sources.malloynb) section.

### Embedding Malloy queries in SQL ([SQL Block Documentation](../language/sql_blocks.malloynb#embedding-malloy-queries-in-an-sql-block-turducken-))

### Case statement improved with  `pick` ([Expression Documentation](../language/expressions.malloynb#pick-expressions))

### Group by on Joined Subtrees

### Date/Timestamp filters and Timezones ([Time Documentation](../language/expressions.malloynb#time-ranges))

### Nested data and Symmetric aggregates  ([Aggregates Documentation](../language/aggregates.malloynb))

### Import ([Import Documentation](../language/imports.malloynb))

### Data styles and rendering ([Rendering Documentation](../visualizations/dashboards.malloynb))
